
  model {

  # Priors and constraints
  logN[1] ~ dnorm(5.6, 0.01)           # Prior for initial population size
  b0 ~ dnorm(0, 0.001)                 # Prior for mean growth rate
  #beta ~ dnorm(0, 0.001)               # Prior for slope parameter
  beta ~ dunif(-10, 10)               # Prior for slope parameter

  sigma.proc ~ dunif(0, 1)             # Prior for sd of state process
  sigma2.proc <- pow(sigma.proc, 2)
  tau.proc <- pow(sigma.proc, -2)
  sigma.obs ~ dunif(0, 1)              # Prior for sd of observation process
  sigma2.obs <- pow(sigma.obs, 2)
  tau.obs <- pow(sigma.obs, -2)

  # Likelihood
  # State process
  for (t in 1:(T-1)){
    r[t] <- b0 + beta * x[t] + epsilon[t]   # Linear model for the population growth rate
    epsilon[t] ~ dnorm(0, tau.proc)         # Random noise of the population growth rate
    logN[t+1] <- logN[t] + r[t]
  }
  # Observation process
  for (t in 1:T) {
    y[t] ~ dnorm(logN[t], tau.obs)
  }

  # Population sizes on real scale
  for (t in 1:T) {
    N[t] <- exp(logN[t])
  }
  }
  